{ config, pkgs, ... }:
let
  ghCredsFile = "${config.home.homeDirectory}/.config/gh/hosts.yml";
  nixNetRcFile = "${config.home.homeDirectory}/.config/nix/netrc";
  nixGithubCredsFile = "${config.home.homeDirectory}/.config/nix/github-access-token.conf";

  do-login = pkgs.writeShellScript "do-login" ''
    set -e
    
    echo -e "machine alpha.pigeon-blues.ts.net\npassword $(${pkgs.gh}/bin/gh auth token)" > ${nixNetRcFile}
    echo "extra-access-tokens = github.com=$(${pkgs.gh}/bin/gh auth token)" > ${nixGithubCredsFile}

    chmod 600 ${nixNetRcFile}
    chmod 600 ${nixGithubCredsFile}
  '';
in
{
  nix.settings = {
    netrc-file = nixNetRcFile;
  };
  nix.extraOptions =
    ''
      # This setting is generated by the nix-github-access-token home-manager module.
      !include github-access-token.conf
    '';

  systemd.user.paths.nix-auto-login = {
    Unit = {
      Description = "Nix auto login to caches";
    };

    Path = {
      # Once per day
      PathChanged = ghCredsFile;
    };

    Install = {
      WantedBy = [ "default.target" ];
    };
  };

  systemd.user.services.nix-auto-login = {
    Unit = {
      Description = "Nix auto login to caches";
    };

    Service = {
      Type = "oneshot";
      ExecStart = do-login;
    };
  };
}
